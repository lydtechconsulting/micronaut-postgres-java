plugins {
    id("io.micronaut.application") version "4.3.2"
    id("io.micronaut.aot") version "4.3.2"
    id("com.github.johnrengelman.shadow") version "8.1.1"
    id("application")
    id("org.graalvm.buildtools.native") version "0.10.1"
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    annotationProcessor("io.micronaut:micronaut-http-validation")
    annotationProcessor("io.micronaut.serde:micronaut-serde-processor")
    annotationProcessor("io.micronaut.data:micronaut-data-processor")
    annotationProcessor("org.projectlombok:lombok:1.18.30")

    implementation("io.micronaut.serde:micronaut-serde-jackson")
    implementation("io.micronaut.data:micronaut-data-jdbc:4.6.2")
    implementation("io.micronaut.sql:micronaut-jdbc-hikari:2.2.6")
    implementation("io.micronaut.liquibase:micronaut-liquibase")
    implementation("io.micronaut:micronaut-management")
    implementation("org.apache.commons:commons-lang3:3.14.0")
    implementation("javax.validation:validation-api:2.0.1.Final")
    implementation("javax.persistence:javax.persistence-api:2.2")
    implementation("org.postgresql:postgresql")

    compileOnly("io.micronaut:micronaut-http-client")
    compileOnly("org.projectlombok:lombok:1.18.30")

    runtimeOnly("ch.qos.logback:logback-classic")
    runtimeOnly("org.yaml:snakeyaml")

    testAnnotationProcessor("org.projectlombok:lombok:1.18.30")
    testCompileOnly("org.projectlombok:lombok:1.18.30")
    testImplementation("io.micronaut:micronaut-http-client")
    testImplementation("org.hamcrest:hamcrest:2.2")
    testImplementation("org.mockito:mockito-core:2.1.0")
    testImplementation("dev.lydtech:component-test-framework:2.11.0")
    testImplementation("io.rest-assured:rest-assured:5.4.0")
    testImplementation("com.h2database:h2:2.2.220")
}

application {
    mainClass.set("demo.DemoApplication")
}

java {
    sourceCompatibility = JavaVersion.toVersion("21")
    targetCompatibility = JavaVersion.toVersion("21")
}

graalvmNative {
    toolchainDetection = false
    binaries {
        main {
            imageName.set('micronaut-postgres-java')
            mainClass = 'demo.DemoApplication'
            buildArgs.add('--verbose')
        }
    }
}

tasks {
    dockerfileNative {
        baseImage('ghcr.io/graalvm/native-image-community:21')
        jdkVersion.set('21')
        args("-Dmicronaut.config.files=file:/application.yml")
    }
    dockerBuildNative {
        images = ["ct/micronaut-postgres-java"]
    }
}

micronaut {
    runtime("netty")
    testRuntime("junit5")
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
    include '**/*Test.*'
}

tasks.register('componentTest', Test) {
    useJUnitPlatform()
    include '**/*CT.*'

    testLogging {
        events "passed", "skipped", "failed"
    }

    systemProperties = System.properties
    environment "TESTCONTAINERS_REUSE_ENABLE", System.getProperty('containers.stayup')
}
